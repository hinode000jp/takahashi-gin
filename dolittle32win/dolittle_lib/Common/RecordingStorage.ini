__Webストレージ＝””。
__Webストレージ:作る=「| f_name user addr;obj |
    obj=""。
    obj:Address = addr。
    obj:Filename = f_name。
    obj:User = user。
    obj:executor=Webクライアント！作る。
    obj：記録＝「| arg ; pram_str num|
        pram_str ="?" + "user=" + (self:User) 
                         +"&"+"filename=" +(self:Filename) 
                         +"&"+"systemtime=" +(システム！システム秒？)。

        「|x|
                pram_str= pram_str + "&data" + (x) +"=" + (arg! (x) 読む) 。
        」！ (arg!要素数？)  繰り返す。
        ret= (self:executor)!(self:Address+pram_str) 読む。     
    self」。
    
obj
」。

__ローカルストレージ＝””。
__ローカルストレージ:作る=「| f_name user addr;obj |
    obj=""。
    obj:Address = addr。
    obj:Filename = f_name。
    obj:User = user。
    obj:executor=テキストファイル！（（obj:Address） + ”/”+（obj:User） + "_" + （obj:Filename））作る。
    obj：記録＝「| arg ; pram_str num|
        pram_str =（システム！年？）+"-" + （システム！月？）+ "-"  +  （システム！日？）
                       + "," +（システム！時刻？） 
                       + "," + (システム！システム秒？)。
        「|x|
         pram_str= pram_str + "," + (arg! (x) 読む) 。
        」！ (arg!要素数？)  繰り返す。
        
        executor!(pram_str) 書く。
    self」。

obj
」。


記録ストレージ＝””。
記録ストレージ：作る＝「| save_addr ;obj arg|
    obj=""。
    obj:Address = save_addr。
    obj:Filename = "SaveData.csv"。
    obj:User = システム：user。
    
    arg = _rest。
  「（arg!要素数？） ＞ 0」！なら「obj:Filename = (arg! 1 読む)」実行。
  「（arg!要素数？） ＞ 1」！なら「obj:User = (arg! 2 読む)」実行。
  
    obj:保存先選択=「|Addr| 
		self:Address = Addr。
      		 「（（Addr!":" 分割）！1 読む）＝＝”http”」！なら「
            		self:processor= __Webストレージ！ (self:Filename) (self:User) (self:Address) 作る。
        	」そうでなければ「
            		self:processor= __ローカルストレージ! （self:Filename） (self:User) （Addr） 作る。      
        	」実行。
    	」。
    
    obj:processor=未定義。
    obj!(obj:Address) 保存先選択。
    
    obj:書く= 「processor！(_rest)  記録。 self」。

obj
」。

// エイリアス設定
 記録サーバー = 記録ストレージ。