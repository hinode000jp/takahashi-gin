システム！”dolittle_lib/mbot/mbotMain” 使う。
システム！”dolittle_lib/mbot/Motor” 使う。
システム！”dolittle_lib/mbot/led” 使う。
システム！”dolittle_lib/mbot/LineSensor” 使う。
システム！”dolittle_lib/mbot/UltraSonicSensor” 使う。
システム！”dolittle_lib/mbot/LightSensor” 使う。
システム！”dolittle_lib/mbot/Button” 使う。
システム！”dolittle_lib/mbot/AnalogPinReader” 使う。
システム！”dolittle_lib/mbot/DigitalPinreader” 使う。
システム！”dolittle_lib/mbot/Sound” 使う。



//////////////////////////////////////////////////
mbot="mbot"。
mbot:命令する=「|_obj ;ret |	ret= __mbot実体!(_obj)命令する 。ret」。
mbot:開く=「|port  |	
	「(port) !=  undef」！なら		「__mbot実体!(port)開く。」
	そうでなければ「__mbot実体!(システム！シリアルポート選択)開く。self」実行。
」。
mbot:閉じる=「__mbot実体!閉じる 。self」。
mbot:待つ=「｜sec｜__mbot実体!(sec) 待つ 。self」。
mbot:ファームウェア?=「|;ret _obj| 
	_obj:mode 	=1。
	_obj:ID 		=0。
	ret = self!(_obj)命令する。
ret」。

////////////////////////////////////////////////////
// ドライバ選択。
////////////////////////////////////////////////////
//mbot:使う＝「|drive opt1 ;obj|　obj=__mbotモータ！(drive)使う。obj」。
mbot:使う＝「|drive opt1;obj|　
				obj = undef。
				「(drive) == "M1"」！なら「obj=__mbotモータ！(drive)使う。」実行。
				「(drive) == "M2"」！なら「obj=__mbotモータ！(drive)使う。」実行。
				「(drive) == "LED"」！なら「obj= (__mbotRGBLED)。」実行。
				「(drive) == "ラインセンサ"」！なら「obj= (__mbotLineSensor)。obj!(opt1)ポート」実行。
				「(drive) == "超音波距離センサ"」！なら「obj= (__UltraSonicSensor)。obj!(opt1)ポート」実行。
				「(drive) == "光センサ"」！なら「obj= (__mbotLightSensor)。「(opt1) != undef」！なら「obj!(opt1)ポート」実行」実行。
				「(drive) == "ボタン"」！なら「obj= (__mbotButton)。「(opt1) != undef」！なら「obj!(opt1)キー検出」実行」実行。
				「(drive) == "アナログ入力"」！なら「obj= (__mbotAnalogPinReader)。「(opt1) != undef」！なら「obj!(opt1)ポート」実行」実行。
				「(drive) == "デジタル入力"」！なら「obj= (__mbotDigitalPinreader)。「(opt1) != undef」！なら「obj!(opt1)ポート」実行」実行。
				「(drive) == "ブザー"」！なら「obj= (__mbotSound)。」実行。

				
				「(obj) == undef」！なら「システム！("drive >> "+(drive)+”:センサが選択できませんでした。処理を続行します。”) 表示ダイアログ。mbot! 2 待つ。」実行。
obj」。


///////////////////////////////////////////////////
// モーター制御系
///////////////////////////////////////////////////
mbot:反転　＝「|motor|　__mbotモータ!(motor)反転。self」。
mbot:回転　＝「|port power time |
	「(time)!=undef」！なら「__mbotモータ！(port)(power)(time)回転」
	そうでなければ「__mbotモータ！(port)(power)回転」実行。
	self」。

mbot:停止　＝「|port ;cmd|　
	「(port) == undef」!なら「__mbotモータ！"M1" 0 回転。__mbotモータ！ "M2" 0 回転。」そうでなければ「__mbotモータ！(port) 0 回転。」実行。
self」。

mbot:前進　＝「|power time ;cmd|　
	「(time) != undef」！なら「__mbotモータ！(power)(time) 前進。」
	　そうでなければ「__mbotモータ！(power)前進。」実行。
self」。

mbot:後退　＝「|power time ;cmd|　
		「(time) != undef」！なら「__mbotモータ！(power)(time)後退。」
	　そうでなければ「__mbotモータ！(power)後退。」実行。
self」。

mbot:右回り　＝「|power time ;cmd|　
		「(time) != undef」！なら「__mbotモータ！(power)(time)  右回り。」
	　そうでなければ「__mbotモータ！(power) 右回り。」実行。
self」。

mbot:左回り　＝「|power time ;cmd|　
		「(time) != undef」！なら「__mbotモータ！(power)(time)  左回り。」
	　そうでなければ「__mbotモータ！(power) 左回り。」実行。
self」。

///////////////////////////////////////////////////
// ブザー
///////////////////////////////////////////////////
mbot:出力＝「|_音　_拍子|  　__mbotSound!(_音)(_拍子) 出力。self」。